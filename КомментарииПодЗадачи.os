//TODO:
// * Настройка параметров адреса ресурса
// * Добавление выбора к существующему файлу. Пример = параметром передана ключевая фраза "/+" - 
//							осуществим поиск по ключевому слову - обновим блок выбора/добавим в случае отсутствия 
// * Добавить возможность формировать шаблон Описания из полей задачи в битрикс

#Использовать cmdline
#Использовать restler

Перем ИмяФайлаШаблона;
Перем юсПарсер;
Перем юсПараметры;
Перем МассивПоиска;
Перем ИмяРазработчика;

Процедура ЗадатьНачальныеНастройки()
	юсПарсер = Новый ПарсерАргументовКоманднойСтроки();
	
	
	Если АргументыКоманднойСтроки.Количество() = 0 Тогда
		юсСообщить("ERROR", "Не заданы аргументы командной строки!");
		
		юсСообщить("INFO", "Принимаемые аргументы: ");
		юсСообщить("*", "/P - Путь к существующему файлу шаблонов, либо путь куда необходимо сохранить новый. Пример С:\filename.st");
		юсСообщить("*", "/T - Токен битрикс 24");
		юсСообщить("*", "/U - ID пользователя битрикс 24. Используется для отбора задач");
		юсСообщить("*", "/S - Обабатываемые ключи замены. Принимает строку разделенную запятыми");
		юсСообщить("*", "/N - Имя разработчика для конфигуратора. Будет подставлено при создании нового шаблона");

		ЗавершитьРаботу(1);
	КонецЕсли;
	ЗаполнитьПараметры();
КонецПроцедуры

Процедура ЗаполнитьПараметры()
	Завершить = Ложь;
	юсПарсер.ДобавитьИменованныйПараметр("/P"); // Путь к файлу шаблонов
	юсПарсер.ДобавитьИменованныйПараметр("/T"); // Токен битрикс 24
	юсПарсер.ДобавитьИменованныйПараметр("/U"); // ID пользователя битрикс 24
	юсПарсер.ДобавитьИменованныйПараметр("/S"); // Строка поиска (будут обработаны только указанные шаблоны)
	юсПарсер.ДобавитьИменованныйПараметр("/N"); // Имя разработчика (будет подставлено в шалон конфигуратора)

	юсПараметры = юсПарсер.Разобрать(АргументыКоманднойСтроки);
	
	//Проверка значений
	
	Если ТипЗнч(юсПараметры["/U"]) <> Тип("Строка") Тогда
		юсСообщить("ERROR", "Не задано значение аргумента /U командной строки!");
		Завершить = Истина;
	КонецЕсли;
	Если ТипЗнч(юсПараметры["/T"]) <> Тип("Строка") Тогда
		юсСообщить("ERROR", "Не задано значение аргумента /T командной строки!");
		Завершить = Истина;
	КонецЕсли;
	Если ТипЗнч(юсПараметры["/P"]) <> Тип("Строка") Тогда
		юсСообщить("ERROR", "Не задано значение аргумента /P командной строки!");
		Завершить = Истина;
	КонецЕсли;
	Если ТипЗнч(юсПараметры["/S"]) <> Тип("Строка") И НЕ СтрДлина(юсПараметры["/S"])Тогда
		юсСообщить("ERROR", "Не задано значение аргумента /S командной строки!");
		Завершить = Истина;
	Иначе
		СыройМассивПоиска = СтрРазделить(юсПараметры["/S"], ",", Ложь);
		МассивПоиска = Новый Массив();
		Для каждого Элемент Из СыройМассивПоиска Цикл
			МассивПоиска.Добавить(СокрЛП(Элемент));
		КонецЦикла;

		Если НЕ МассивПоиска.Количество() Тогда
			юсСообщить("ERROR", "Не удалось разобрать строку поиска!");	
		КонецЕсли;
	КонецЕсли;
	Если ТипЗнч(юсПараметры["/N"]) <> Тип("Строка") И НЕ СтрДлина(юсПараметры["/N"])Тогда
		юсСообщить("ERROR", "Не задано значение аргумента /N командной строки!");
		Завершить = Истина;
	Иначе
		ИмяРазработчика = юсПараметры["/N"];
	КонецЕсли;
	Если Завершить Тогда
		ЗавершитьРаботу(1);
	КонецЕсли;
	
КонецПроцедуры

Процедура юсСообщить(ТипСообщения, ТекстСообщения)
	ТекстСообщения = "[" + ТекущаяДата() + "] " + "[" + ТипСообщения + "] " + ТекстСообщения;
	Сообщить(ТекстСообщения);
КонецПроцедуры

Процедура ПолучитьСтрокуЗадач() // &filter[REAL_STATUS]=5

	юсСообщить("INFO", "Получаю задачи из bitrix 24");
	Токен = юсПараметры["/T"]; // 3d361t7rnvn5guqk
	ID = юсПараметры["/U"]; // 13083	
	
	Соединение = Новый HTTPСоединение("https://b24.esteam.band", 443);
	
	Клиент = Новый КлиентВебAPI();
	Клиент.ИспользоватьСоединение(Соединение);
	Запрос = "/rest/13083/" + Токен + "/tasks.task.list?filter[RESPONSIBLE_ID]=" + ID + "&filter[!REAL_STATUS]=5&select[0]=ID&select[1]=TITLE";
	Результат = Клиент.Получить(Запрос);
	
	Если НЕ Результат["result"]["tasks"].Количество() Тогда
		юсСообщить("INFO", "Задачи не найдены!");
		ЗавершитьРаботу(1);			
	КонецЕсли;

	БлокЗамены = "<?""""Список задач: """", ВыборВарианта";
	Для каждого Элемент Из Результат["result"]["tasks"] Цикл
		Элемент["title"] = СтрЗаменить(Элемент["title"], """", "");
		БлокЗамены = БлокЗамены + ", " + """""" + Элемент["title"] + """""" + ", " + """""" + Элемент["id"] + """""";
	КонецЦикла;
	БлокЗамены = БлокЗамены + ">";
	
	юсПараметры.Вставить("блок", БлокЗамены);
КонецПроцедуры

Функция НужноОбработатьСтроку(текСтрока)
	позицияКлюча = 4; // в структуре шаблона ключ находится после 3-ей запятой 
	МассивПодстрок = СтрРазделить(текСтрока, ",", Истина);
	НужноОбработать = Ложь;
	
	Если МассивПодстрок.Количество() >= позицияКлюча Тогда
			НужноОбработать = МассивПоиска.Найти(СтрЗаменить((МассивПодстрок[3]), """", "")) <> Неопределено;
	КонецЕсли;

	Возврат НужноОбработать;
КонецФункции

Процедура ПолучитьФайлШаблона()
	ИмяФайлаШаблона = юсПараметры["/P"];
	Файл = Новый Файл(ИмяФайлаШаблона);
	Если Файл.Существует() Тогда
		ОбновитьШаблон();
	Иначе
		СоздатьНовыйФайлШаблона();
	КонецЕсли;
КонецПроцедуры
Процедура ОбновитьШаблон()
	юсСообщить("INFO", "Обновляю файл шаблонов");
	ИмяФайлаШаблона = юсПараметры["/P"];
	БлокЗамены = юсПараметры["блок"];
	
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.Прочитать(ИмяФайлаШаблона);
	Для а = 0 По ТекстДок.КоличествоСтрок() Цикл
		//БлокНайден = ;
		позицияНач = 0;
		КонецБлока = 0;
		текСтрока = ТекстДок.ПолучитьСтроку(а);
		Если НужноОбработатьСтроку(текСтрока) Тогда
			//БлокНайден = Ложь;
			Пока СтрНайти(текСтрока, "<?", , позицияНач) <> 0 Цикл
				НачалоБлока = СтрНайти(текСтрока, "<?", , позицияНач);
				КонецБлока = СтрНайти(текСтрока, ">", , НачалоБлока);
				Если НачалоБлока = 0 ИЛИ КонецБлока = 1 Тогда
					позицияНач = КонецБлока;
					Продолжить;
				КонецЕсли;
				блок = Сред(текСтрока, НачалоБлока, КонецБлока - НачалоБлока + 1);
				Если СтрНайти(блок, " ВыборВарианта,") > 0 Тогда
					НоваяСтрока = СтрЗаменить(текСтрока, блок, БлокЗамены);
					ТекстДок.ЗаменитьСтроку(а, НоваяСтрока);
					// удалим ключ из массиваПоиска в случае успешной замены
					удаляемыйКлюч = СтрЗаменить(СтрРазделить(НоваяСтрока, ",", Истина)[3], """", "");
					Если МассивПоиска.Найти(СокрЛП(удаляемыйКлюч)) <> Неопределено Тогда
						МассивПоиска.Удалить(МассивПоиска.Найти(СокрЛП(удаляемыйКлюч)));
					КонецЕсли;
					//БлокНайден = Истина;
					Прервать;
				КонецЕсли;
				позицияНач = КонецБлока;
				
			КонецЦикла;
		КонецЕсли;
		// Если Не БлокНайден Тогда
		// 	//Найдем начало подставляемого шаблона 1с и вставим свой блок
		// 	ПоложениеКлюча = 0; 
		// 	Для Каждого Ключ Из МассивПоиска Цикл
		// 		ПоложениеКлюча = СтрНайти(текСтрока, Ключ + """,");
		// 		Если ПоложениеКлюча > 0 Тогда
		// 			Прервать;
		// 		КонецЕсли;
		// 	КонецЦикла;
		// 	Если ПоложениеКлюча > 0 Тогда
		// 		лЧасть = Лев(текСтрока, ПоложениеКлюча + СтрДлина(Ключ + """,") + 3); //3 - кавычка + "//"
		// 		пЧасть = Прав(текСтрока, СтрДлина(текСтрока) - СтрДлина(лЧасть));

		// 		ТекстДок.ЗаменитьСтроку(а, лЧасть + БлокЗамены + пЧасть);
		// 	КонецЕсли;
		// КонецЕсли;
	КонецЦикла;

	// TODO: добавить не обработанные ключи в файл (оставшиеся в массивеПоиска)
	МассивДобавить = Новый Массив;
	Для Каждого Элемент Из МассивПоиска Цикл
		МассивДобавить.Добавить(ЗаполнитьШаблон(Элемент));
	КонецЦикла;
	
	СтрДобавить = СтрСоединить(МассивДобавить, ", " + Символы.ПС);
	Если НЕ ПустаяСтрока(СтрДобавить) Тогда
		СтрокаВставки = 4; // предполагаем, что первые три строки открывают файл 
		ТекстДок.ЗаменитьСтроку(СтрокаВставки , СтрДобавить + "," + Символы.ПС + 
												ТекстДок.ПолучитьСтроку(СтрокаВставки) + Символы.ПС);
		СтрокаСоСчетчиком = ТекстДок.ПолучитьСтроку(2);
		Счетчик = Число(СтрЗаменить(СтрЗаменить(СтрокаСоСчетчиком, ",", ""), "{", "")); // выберем счетчик
		ТекстДок.ЗаменитьСтроку(2, "{" + (Счетчик + МассивПоиска.Количество()) + ","); 

	КонецЕсли;
	ТекстДок.Записать(ИмяФайлаШаблона);
	юсСообщить("INFO", "Обновление завершено.");
КонецПроцедуры
Функция ЗаполнитьШаблон(Ключ)
	// ES, 8583,  Несторук Сергей 08.10.2019 {
	// }Несторук Сергей

	БлокЗамены = юсПараметры["блок"];
	Строка = "{0,
	|{""b24_comment[%1]"",0,1,""%1"",""//{ES, %2 %3 <?"""""""", ДатаВремя, """"ДЛФ=DT"""">
	|<?>
	|//} %3 <?"""""""", ДатаВремя, """"ДЛФ=DT""""> ""}
	|}"; 
	Возврат СтрШаблон(Строка, Ключ, ?(БлокЗамены = "", "", БлокЗамены), ИмяРазработчика); // Получать имя из битрикса или из параметров команды?
КонецФункции
Процедура СоздатьНовыйФайлШаблона()
	юсСообщить("INFO", "Файл не найден. Создаю новый файл шаблонов");
	ИмяФайлаШаблона = юсПараметры["/P"];
	БлокЗамены = юсПараметры["блок"];
	
	Текст = Новый ТекстовыйДокумент();
	Текст.ДобавитьСтроку("{1,");
	Текст.ДобавитьСтроку(СтрШаблон("{%1,", МассивПоиска.Количество()));
	Текст.ДобавитьСтроку("{""b24_comment"",1,0,"""",""""},");
	МассивШаблонов = Новый Массив();
	Для Каждого Ключ Из МассивПоиска Цикл
		МассивШаблонов.Добавить(ЗаполнитьШаблон(Ключ));
	КонецЦикла;
	Текст.ДобавитьСтроку(СтрСоединить(МассивШаблонов, ", " + Символы.ПС));
	Текст.ДобавитьСтроку("}");
	Текст.ДобавитьСтроку("}");
	
	Текст.Записать(ИмяФайлаШаблона);
	
	юсСообщить("INFO", "Создан файл шаблона: " + ИмяФайлаШаблона);
КонецПроцедуры

ЗадатьНачальныеНастройки();
ПолучитьСтрокуЗадач();
ПолучитьФайлШаблона();